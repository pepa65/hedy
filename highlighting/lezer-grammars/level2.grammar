@top Program { eol* (Command eol+)* Command? }
Command {
    Assign | Ask | Print | Play | Turtle | Sleep | ErrorInvalid
}

Assign { Text is+ Text+ }
Ask { Text is+ ask+ Text+ }
Print { print+ Text+ }
Sleep { sleep+ Text* }
Play { play+ Text+ }

Turtle[@isGroup=turtle] {
    Forward { forward+ Text } |
    Turn { turn+ Text } |
    Color { color+ Text }
}

ErrorInvalid[@dynamicPrecedence=-10] { Text+ }

@external specialize { Text } specializeKeyword from "./tokens" {
    ask[@name="ask"]
}

@external extend { Text } extendKeyword from "./tokens" {
    print[@name="print"],
    forward[@name="forward"],
    turn[@name="turn"],
    color[@name="color"],
    sleep[@name="sleep"],
    play[@name="play"],
    is[@name="is"]
}

@tokens {
    Comment { "#" ![\n]* }
    eol { "\n" }
    space { " " }
    Text { ![\n #!?.]+ }
    SpecialChar { "!" | "?" | "." }     
}

@skip { Comment | space | SpecialChar}
